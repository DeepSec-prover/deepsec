/* Wide Mouthed Frog Symmetric Key

 1.  A	->	S:  	{B, Kab}Kas
 2.  S	->	B:  	{A, Kab}Kbs
 Strong secrecy of Kab
 3.  B      ->     : {m}Kab versus {m}K with K fresh
*/

free a1.
free b1.
free a2.
free b2.

free c.

free ok.

fun senc/2.
reduc sdec(senc(x,y),y) -> x.

/*
    Description of role A played:
 - on channel ca
 - by a with key kas (shared with the server S)
 - with responder b
*/

let processA(a,kas,b) =
  new kab;
  out(c,senc((b,kab),kas)).

/*
    Description of role B played:
 - on channel cb
 - by b with key kbs (shared with the server S)
 - with initiator a
*/

let processB(b,kbs,a)  =
  in(c,yb);
  let (ya,yab)= sdec(yb,kbs) in
  if ya = a then 0.

let processBreal(b,kbs,a)  =
  in(c,yb);
  let (ya,yab)= sdec(yb,kbs) in
  if ya = a then
  out(c,senc(ok,yab)).


let processBideal(b,kbs,a)  =
  in(c,yb);
  let (ya,yab)= sdec(yb,kbs) in
  if ya = a then
 	new k;
	out(c,senc(ok,k)).

/*
    Description of role S played:
 - on channel cs
 - to answer a request coming from a (with key kas)
 - who wants to communicate with b (with key kbs)
*/

let processS(a,kas,b,kbs) =
	in(c,zs);
	let (zb,zab) = sdec(zs,kas) in
	if zb =b then
	out(c,senc((a,zab),kbs)).

/*
   			Main
*/

let Preal =
  new kas1; new kbs1; new kas2; new kbs2;
( processA(a1,kas1,b1) |  processBreal(b1,kbs1,a1) | processS(a1,kas1,b1,kbs1) |
  processA(a2,kas2,b2) |  processB(b2,kbs2,a2) | processS(a2,kas2,b2,kbs2)
).


let Pideal =
  new kas1; new kbs1; new kas2; new kbs2;
( processA(a1,kas1,b1) |  processBideal(b1,kbs1,a1) | processS(a1,kas1,b1,kbs1) |
  processA(a2,kas2,b2) |  processB(b2,kbs2,a2) | processS(a2,kas2,b2,kbs2)
).

query trace_equiv(Preal,Pideal).
