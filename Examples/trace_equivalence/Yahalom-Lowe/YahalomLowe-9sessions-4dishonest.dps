(*
  Yahalom - Lowe protocol (without the last message)
  A, B, S :  principal
  Na, Nb : fresh numbers
  Kas, Kbs, Kab :   key

  1.   A -> B : A, Na
  2.   B -> S : {A, Na, Nb}Kbs
  3.   S -> A : {B, Kab, Na, Nb}Kas
  4.   S -> B : {A, Kab}Kbs
*)

//Channels :

free ca1,cb1,cs1.
free ca2,cs2,cb2.
free ca3,cs3,cb3.
free ca4,cs4,cb4.
free ca5,cs5,cb5.
free ca6,cs6,cb6.
free ca7,cs7,cb7.
free ca8,cs8,cb8.
free ca9,cs9,cb9.
free ca10,cs10,cb10.
free ca11,cs11,cb11.

//Public data :
free a,b,c,d,s.
free s1,s2,kcs.

fun senc/2.
reduc sdec(senc(x,y),y) -> x.

// Alice:
let A(ca,a,b,kas) =
  new na;
  out(ca,(a,na));
  in(ca,x0);
  let (=b,xkab,=na,xnb) = sdec(x0,kas) in
  0.

// Bob :
let B(cb,b,a,kbs) =
  in(cb,y0);
  let (=a,yna) = y0 in
  new nb;
  out(cb,senc((a,yna,nb),kbs));
  in(cb,y1);
  let (=a,ykab) = sdec(y1,kbs) in
  0.

// Server (+ property):
let S1(cs,a,b,kas,kbs) =
  in(cs,z0);
  let (=a,zna,znb) = sdec(z0,kbs) in
  out(cs,(senc((b,s1,zna,znb),kas),senc((a,s1),kbs))).

let S2(cs,a,b,kas,kbs) =
  in(cs,z0);
  let (=a,zna,znb) = sdec(z0,kbs) in
  out(cs,(senc((b,s2,zna,znb),kas),senc((a,s2),kbs))).

//Server:
let S(cs,a,b,kas,kbs) =
  in(cs,z0);
  new kab;
  let (=a,zna,znb) = sdec(z0,kbs) in
  out(cs,(senc((b,kab,zna,znb),kas),senc((a,kab),kbs)) ).


// Protocols :
let P =
  new kas; new kbs; new kds;
  (
    A(ca1,a,b,kas) | S1(cs1,a,b,kas,kbs) | B(cb1,b,a,kbs) |
    A(ca2,a,c,kas) | S(cs2,a,c,kas,kcs) |
    S(cs3,c,b,kcs,kbs) | B(cb3,c,b,kbs) |
    A(ca4,b,a,kbs) | S(cs4,b,a,kbs,kas) | B(cb4,a,b,kas) |
    A(ca5,b,c,kbs) | S(cs5,b,c,kbs,kcs) |
    S(cs6,c,a,kcs,kas) | B(cb6,c,a,kas) |
    A(ca7,a,d,kas) | S(cs7,a,d,kas,kds) | B(cb7,d,a,kds) |
    A(ca8,a,b,kas) | S(cs8,a,b,kas,kbs) | B(cb8,b,a,kbs) |
    A(ca9,a,b,kas) | S(cs9,a,b,kas,kbs) | B(cb9,b,a,kbs)
  ).


let Q =
  new kas; new kbs; new kds;
  (
    A(ca1,a,b,kas) | S2(cs1,a,b,kas,kbs) | B(cb1,b,a,kbs) |
    A(ca2,a,c,kas) | S(cs2,a,c,kas,kcs) |
    S(cs3,c,b,kcs,kbs) | B(cb3,c,b,kbs) |
    A(ca4,b,a,kbs) | S(cs4,b,a,kbs,kas) | B(cb4,a,b,kas) |
    A(ca5,b,c,kbs) | S(cs5,b,c,kbs,kcs) |
    S(cs6,c,a,kcs,kas) | B(cb6,c,a,kas) |
    A(ca7,a,d,kas) | S(cs7,a,d,kas,kds) | B(cb7,d,a,kds) |
    A(ca8,a,b,kas) | S(cs8,a,b,kas,kbs) | B(cb8,b,a,kbs) |
    A(ca9,a,b,kas) | S(cs9,a,b,kas,kbs) | B(cb9,b,a,kbs)
  ).


query trace_equiv(P,Q).
