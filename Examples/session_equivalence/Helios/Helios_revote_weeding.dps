(* New version of Helios *)

free id1.
free id2.
free id3.

free c.

free replayedBallot.

free yes.
free no.

(* Randomized asymmetric encryption *)

fun aenc/3.
fun pk/1.

reduc adec(sk, aenc(pk(sk), sr, xm)) -> xm.

(* Signature *)

fun sign/2.
fun vk/1.
reduc checksign(vk(sk), sign(sk,m)) -> m.

(* The zero knowledge proof definition. *)

fun zkp/3.
fun zkpok/0.
reduc checkzkp(zkp(xr, xv, aenc(xpk, xr, xv)), aenc(xpk, xr, xv)) -> zkpok.

(* The natural number model *)

fun zero/0.
fun s/1.

(* The voting process *)

let Voter(sk,v,pkE) =
    new r;
    let ballot = aenc(pkE, r, v) in
    let zk = zkp(r, v, ballot) in
    out(c, sign(sk, (ballot, zk))).

(* The Tally *)

let Outcome(prv_ch,skE) =
  in(prv_ch,z);
  let (vote1,vote2,vote3,nb_vote) = z in

  (* We check that we have 7 votes. *)
  if nb_vote = s(s(s(s(s(s(s(zero))))))) then

  (* We check the replayed Ballot *)
  if vote1 = vote2
  then out(c,replayedBallot)
  else if vote1 = vote3
  then out(c,replayedBallot)
  else if vote2 = vote3
  then out(c,replayedBallot)
  else
    (* No replay so we output *)
    let xv1 = adec(skE,vote1) in  (* Due to the fact that adec is a destructor, we are ensured that at least one vote of id1 and id2 were counted. *)
    let xv2 = adec(skE,vote2) in
    (out(c,xv1) | out(c,xv2) | out(c,adec(skE,vote3))). (* The decryption of the vallot of id3 is done at the lvl of the output allowing the attacker NOT to vote. *)

let SubTally(prv_ch,vk1,vk2,vk3) =
  in(prv_ch,z);
  let (vote1,vote2,vote3,nb_vote) = z in

  in(c, x);
  let (yid, ysign) = x in
    let ((zblob1,zzkp1),=id1) = (checksign(vk1,ysign), yid) in
      if checkzkp(zzkp1, zblob1) = zkpok
      then out(prv_ch,(zblob1,vote2,vote3,s(nb_vote)))
      else out(prv_ch,(vote1,vote2,vote3,s(nb_vote)))
    else
      let ((zblob2,zzkp2),=id2) = (checksign(vk2,ysign), yid) in
        if checkzkp(zzkp2, zblob2) = zkpok
        then out(prv_ch,(vote1,zblob2,vote3,s(nb_vote)))
        else out(prv_ch,(vote1,vote2,vote3,s(nb_vote)))
      else
        let ((zblob3,zzkp3),=id3) = (checksign(vk3,ysign), yid) in
          if checkzkp(zzkp3, zblob3) = zkpok
          then out(prv_ch,(vote1,vote2,zblob3,s(nb_vote)))
          else out(prv_ch,(vote1,vote2,vote3,s(nb_vote)))
        else out(prv_ch,(vote1,vote2,vote3,s(nb_vote)))
  else out(prv_ch,(vote1,vote2,vote3,s(nb_vote))).

let Tally(skE,vk1,vk2,vk3) =
  new prv_ch;
  (
    out(prv_ch,(zero,zero,zero,zero))
  | Outcome(prv_ch,skE)
  | !^7 SubTally(prv_ch,vk1,vk2,vk3)
  ).

let VotingSystem(vote1,vote2) =
  new skE;
  out(c,pk(skE));
  new sk1;
  new sk2;
  new sk3;
  out(c,sk3);
  out(c,vk(sk1));
  out(c,vk(sk2));
  (Voter(sk1,vote1,pk(skE)) | Voter(sk2,vote2,pk(skE)) | Tally(skE,vk(sk1),vk(sk2),vk(sk3))).

let VotingSystem_twice1(vote1,vote2) =
  new skE;
  out(c,pk(skE));
  new sk1;
  new sk2;
  new sk3;
  out(c,sk3);
  out(c,vk(sk1));
  out(c,vk(sk2));
  ( !^2 Voter(sk1,vote1,pk(skE)) | Voter(sk2,vote2,pk(skE)) | Tally(skE,vk(sk1),vk(sk2),vk(sk3))).

(* Should find an attack. *)
query session_equiv(VotingSystem_twice1(yes,no),VotingSystem_twice1(no,yes)).

(* Should not find an attack. *)
query session_equiv(VotingSystem(yes,no),VotingSystem(no,yes)).
