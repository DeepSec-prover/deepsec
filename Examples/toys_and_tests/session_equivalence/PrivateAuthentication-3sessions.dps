(* Private Authentication Protocol

 1. A -> B: {Na, pub(A)}pub(B)
 2. B -> A: {Na, Nb,pub(B)}pub(A) if B wants to communicate with A
                  {Nb}pub(B) otherwise
*)

free ca1,ca2,ca3.
free cb1,cb2,cb3.
free c.

free ska, skb, skc [private].

fun aenc/2.
fun pk/1.

reduc adec(aenc(x,pk(y)),y) -> x.

/*
    Description of role A played:
 - on channel ca
 - by the agent with private key ska
 - with the agent with public key pkb
*/

let processA(ca,ska,pkb) =
  new na;
  out(ca,aenc((na,pk(ska)),pkb));
  in(ca,x).


/*
    Description of role B played:
 - on channel cb
 - by the agent with private key skb
 - with the agent with public key pka
*/

let processB(cb,skb,pka) =
  in(cb,yb);
  new nb;
  let (yna,=pka) = adec(yb,skb) in
    out(cb,aenc((yna,nb,pk(skb)),pka))
  else
    new k; out(cb,k).

/*
 Main
*/

let ProcessAB =
  out(c,pk(ska));
  out(c,pk(skb));
  out(c,pk(skc));
  (
    processA(ca1,ska,pk(skb)) | processB(cb1,skb,pk(ska)) | // B expects to talk to A
    processA(ca2,ska,pk(skb)) | processB(cb2,skb,pk(ska)) | // B expects to talk to A
    processA(ca3,skc,pk(ska)) | processB(cb3,ska,pk(skc))   // A expects to talk to C (C is not an intruder)
  ).

let ProcessCB =
  out(c,pk(ska));
  out(c,pk(skb));
  out(c,pk(skc));
  (
    processA(ca1,skc,pk(skb)) | processB(cb1,skb,pk(skc)) | // B expects to talk to C
    processA(ca2,ska,pk(skb)) | processB(cb2,skb,pk(ska)) | // B expects to talk to A
    processA(ca3,skc,pk(ska)) | processB(cb3,ska,pk(skc))   // A expects to talk to C (C is not an intruder)
  ).


query session_equiv(ProcessAB,ProcessCB).
