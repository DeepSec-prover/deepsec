(* Wide Mouthed Frog protocol (without timestamps)
# A -> S: A, {B,Kab}Kas
# S -> B: {A,Kab}Kbs
*)

free a,b,c.

free s1,s2, kcs.
free kas, kbs [private].

free ch.

fun senc/2.
reduc sdec(senc(x,y),y) -> x.

let A1(ca,a,b,kas) =
  out(ca, (a, senc((b,s1),kas))).

let A2(ca,a,b,kas) =
  out(ca, (a, senc((b,s2),kas))).

let A(ca,a,b,kas) =
  new kab;
  out(ca, (a, senc((b,kab),kas))).

let S(cs,a,b,kas,kbs) =
  in(cs, x);
  let (=a,xenc) = x in
  let (=b,xk) = sdec(xenc,kas) in
  out(cs, senc((a,xk),kbs)).

let B(cb,b,a,kbs) =
  in(cb,y);
  let (ya,yk) = sdec(y,kbs) in 0.

let P =
  A1(ch,a,b,kas) | S(ch,a,b,kas,kbs) | B(ch,b,a,kbs) |
  A(ch,a,b,kas) | S(ch,a,b,kas,kbs) | B(ch,b,a,kbs) |
  A(ch,a,b,kas) | S(ch,a,b,kas,kbs) | B(ch,b,a,kbs).

let Q =
  A2(ch,a,b,kas) | S(ch,a,b,kas,kbs) | B(ch,b,a,kbs) |
  A(ch,a,b,kas) | S(ch,a,b,kas,kbs) | B(ch,b,a,kbs) |
  A(ch,a,b,kas) | S(ch,a,b,kas,kbs) | B(ch,b,a,kbs).

query trace_equiv(P,Q).
